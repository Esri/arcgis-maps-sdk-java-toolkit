plugins {
  id "java-library"
  id "idea"
  id "eclipse"
  id "com.google.osdetector" version "1.6.2"
}

ext {
  platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os
}

group "com.esri.arcgisruntime"
version = "100.7.0"
archivesBaseName = "arcgis-java-toolkit"

idea.module.downloadJavadoc = true
eclipse.classpath.downloadJavadoc = true

compileJava.options.encoding = 'UTF-8'

repositories {
  jcenter()
  maven {
    url = 'https://esri.bintray.com/arcgis'
  }
}

sourceSets {
  main {
    resources.srcDirs = ['src/main/java']
  }
  integrationTest {
    java.srcDirs = ['src/integration-test/java']
    compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    resources.srcDirs = ["src/integration-test/resources"]
    runtimeClasspath += output + compileClasspath
  }
}

configurations {
  natives
}

dependencies {
  implementation "com.esri.arcgisruntime:arcgis-java:100.7.0"
  natives "com.esri.arcgisruntime:arcgis-java-jnilibs:100.7.0"
  natives "com.esri.arcgisruntime:arcgis-java-resources:100.7.0"
  implementation "org.openjfx:javafx-base:11:$platform"
  implementation "org.openjfx:javafx-graphics:11:$platform"
  implementation "org.openjfx:javafx-controls:11:$platform"
  implementation "org.openjfx:javafx-fxml:11:$platform"
  implementation "org.openjfx:javafx-web:11:$platform"
  implementation "org.openjfx:javafx-media:11:$platform"

  testImplementation "org.junit.jupiter:junit-jupiter-api:5.1.0"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.1.0"
  testCompileOnly "junit:junit:4.12"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.1.0"

  testImplementation "org.testfx:testfx-core:4.0.15-alpha"
  testImplementation "org.testfx:testfx-junit:4.0.15-alpha"
  testImplementation "org.hamcrest:hamcrest:2.1"
}

task copyNatives(type: Copy) {
  description = "Copies the arcgis native libraries into USER_HOME/.arcgis for development."
  group = "build"
  configurations.natives.asFileTree.each {
    from(zipTree(it))
  }
  into "${System.properties.getProperty("user.home")}/.arcgis/100.7.0"
}

task integrationTest(type: Test) {
  description = 'Runs the integration tests.'
  group = 'verification'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.withType(Test.class) {
  dependsOn copyNatives
}

task javadocJar(type: Jar) {
  dependsOn javadoc
  description = "Creates a javadoc jar."
  group = "Documentation"
  archiveClassifier.set('javadoc')
  from javadoc.destinationDir
}

wrapper {
  gradleVersion = '5.6'
}
